import org.apache.tools.ant.filters.EscapeUnicode

plugins {
    id 'org.jetbrains.intellij' version '0.7.3'
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
}

group 'com.guohanlin.JsonToAnyLanguage'
version '1.1.0-FIX-IU.191'

repositories {
    mavenCentral()
    maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation('com.alibaba:fastjson:1.2.73')
    implementation("com.squareup.retrofit2:retrofit:2.9.0")
    implementation('com.squareup.retrofit2:converter-gson:2.9.0')
    implementation('com.squareup.retrofit2:adapter-rxjava2:2.9.0')
    implementation('com.squareup.retrofit2:converter-scalars:2.9.0')
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '2019.1'
    pluginName 'JsonToAnyLanguage'
    updateSinceUntilBuild false
}

/**
 * 获取插件描述
 * @return
 */
def readDescription() {
    def descriptionFile = file("$projectDir/DESCRIPTION.md")
    String content = ""
    descriptionFile.eachLine { line ->
        content = "$content\n$line"
    }
    return content
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(ProcessResources) {
    filesMatching('**/*.properties') {
        filter EscapeUnicode
    }
}

patchPluginXml {
    pluginDescription = readDescription()
    changeNotes """
    1、兼容数组解析
    </br>
    1.Compatible with array parsing
    """
}